/**
*AUTHOR : Johan Zicola
*DATE: 2018-01-31
*
*INFORMATION:
*This macro analyses different parameters of images containing 1 rosette of Arabidopsis thaliana
*(should work with other species). The macro requires as input a directory containing the 
*images (RGB with tiff or jpg extension). Different directories are generated:
* "binary_images", contains the thresholded images
* "red_images" and "green_images" which contain red and green RGB channels, respectively
* the "results" directory contains txt files (tab-separated values) which contains the results of 
*the analysis for each image (in red and green channels)
*
* This macro requires as input the directory containing the images to analyze (RGB images in JPG or TIFF format)
* The segmentator generated with SIOX: Simple Interactive Object Extraction plugin (see README.md for protocol)
*/

macro "Rosette analysis"{   

    // Generate GUI for user input
    Dialog.create("Dialog Windows");
    source_folder = getDirectory("Select source directory:");
	segmentator = File.openDialog("Select the segmentator:");
    
	//Get Date and time of the system
	function get_time() {
	     MonthNames = newArray("Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec");
	     DayNames = newArray("Sun", "Mon","Tue","Wed","Thu","Fri","Sat");
	     getDateAndTime(year, month, dayOfWeek, dayOfMonth, hour, minute, second, msec);
	     TimeString ="Date: "+DayNames[dayOfWeek]+" ";
	     if (dayOfMonth<10) {TimeString = TimeString+"0";}
	     TimeString = TimeString+dayOfMonth+"-"+MonthNames[month]+"-"+year+"\nTime: ";
	     if (hour<10) {TimeString = TimeString+"0";}
	     TimeString = TimeString+hour+":";
	     if (minute<10) {TimeString = TimeString+"0";}
	     TimeString = TimeString+minute+":";
	     if (second<10) {TimeString = TimeString+"0";}
	     TimeString = TimeString+second;
	     print(TimeString);
	}


    // Defined whether the filename provided contains either of the extensions
    // defined in 'extensions' array
    function isImage(filename) {
        extensions = newArray("jpg", "tiff","tif");
        result = false;
        for (i=0; i<extensions.length; i++) {
            if (endsWith(toLowerCase(filename), "." + extensions[i]))
            result = true;
        }
        return result;
    }
    
    // Close all files (images, results, summary, ...) open in ImageJ
    function closeAll(){
        list = getList("window.titles");
        for (i=0; i<list.length; i++){
            winame = list[i];
            selectWindow(winame);
            run("Close");
        }
    }

    // the channel (either blue or red) and save it in "green_images" or "red_images" directories
    function extract_color(image, color){
        list_source = getFileList(source_folder);
        if (color == "red"){
        	code = "1-1";
        }
        if (color == "green"){
        	code = "2-2";
        }
        setBatchMode(true);
        for (i=0; i < list_source.length; i++){
            source = list_source[i];
            if (isImage(source)){
                open(source_folder+source);
                run("RGB Stack");
                run("Duplicate...", "duplicate range="+code);
                saveAs("Tiff",source_folder+File.separator+color+"_images"+File.separator+source);
                run("Close All"); //important to use "Close All" instead of close() to avoid RAM filling
            }
        setBatchMode(false);
        }
    }

	// rosette segmentation step using SIOX: Simple Interactive Object Extraction plugin
	function rosette_segmentation(source_folder){
		list_source = getFileList(source_folder);
	    setBatchMode(true);
	    for (i=0; i < list_source.length; i++){
	        source = list_source[i];
	        if (isImage(source)){
				// Open image
				open(source_folder+File.separator+source);
	        	// Open SIOX and perform segmentation of the open image using
		    	// the segmentator previously trained by the user
		    	run("Apply saved SIOX segmentator", "browse="+segmentator+" siox="+segmentator);
		    	// Save binary file generated
		    	saveAs("Tiff",source_folder+File.separator+"binary_images"+File.separator+source);
		    	//close();
		    	run("Close All");
	        }
	    }
	   setBatchMode(false);
	}
    
   
    // Analyze particles binary images in "binary_images" directory and extract 
    // pixel information from the "green images" in "green_images" directory.
    // Generates a binary image from the particles analyzed and save it in 
    // "analysed_images" directory. Save the "result" file generated by "Analyze Particles"
    // and save it in "results" directory (under the name <image_name>_Results.txt.
    function particle_analysis(source_folder, color){
        list_source = getFileList(source_folder+File.separator+color+"_images");
        list_binary = getFileList(source_folder+File.separator+"binary_images");
        
        setBatchMode(true);
        for (i=0; i < list_source.length; i++){
            source = list_source[i];
            binary = list_binary[i];
            //Get the name of the file opened with its .tiff extension => will be used to name results output
            dotIndex = indexOf(source, ".");
            title = substring(source, 0, dotIndex);
            open(source_folder+File.separator+color+"_images"+File.separator+source);
            rename(title);
            open(source_folder+File.separator+"binary_images"+File.separator+binary);
            run("Set Measurements...", "area mean standard min kurtosis area_fraction redirect="+title+" decimal=3");
            run("Analyze Particles...", "show=Masks display exclude clear summarize add");
            selectWindow("Results");
            saveAs("Results", source_folder+File.separator+"results"+File.separator+source+"_"+color+"_Results.txt");
            run("Close All");
        }
        setBatchMode(false);
}
    
 
    // Create directories
    File.makeDirectory(source_folder+File.separator+"binary_images"+File.separator);
    File.makeDirectory(source_folder+File.separator+"results");
	File.makeDirectory(source_folder+File.separator+"green_images"+File.separator);
    File.makeDirectory(source_folder+File.separator+"red_images"+File.separator);

    // launch function

    // Extract red channel
    extract_color(source_folder, "red");
    
    // Extract green channel
    extract_color(source_folder, "green");

    // Segment rosette based on green image
    rosette_segmentation(source_folder);
    
    // Analysis from red channel images
    particle_analysis(source_folder, "red");
    
    // Analysis from green channel images
    particle_analysis(source_folder, "green");
        
    // Generates a log file in the input directory (defined by user) containing
    // the input directory, the threshold method used and the date 
    print("folder processed processed: "+source_folder);
	print("Segmentator used : "+segmentator);
	get_time(); //calls date and time from defined function get_time
	selectWindow("Log");
	saveAs("Text", source_folder+File.separator+"Log.txt");
	run("Close");

	closeAll();
	
}
